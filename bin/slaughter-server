#!/usr/bin/perl -w
#
#  This script allows the slaughter client to be triggered remotely.
#
#  TODO: Document.
#
# Steve
# --

use strict;
use warnings;

use File::Temp qw/ tempfile /;
use HTTP::Daemon;
use HTTP::Status;

#
#  Get the secret for this host
#
my $secret = readFile( "/etc/slaughter/secret" );


my $d = new HTTP::Daemon( LocalPort => 3303,
                          ReuseAddr => 1);

if ( ! defined( $d ) )
{
    print "Failed to bind\n";
    exit 1;
}


while (my $c = $d->accept)
{
    while (my $r = $c->get_request)
    {
        if ($r->method eq 'GET')
        {
            my $path = $r->url->path || undef;

            if ( $path =~ /^\/secret\/([^\/]+)\/*/ )
            {
                my $supplied = $1;
                if ( $supplied eq $secret )
                {
                    my (undef, $filename) = tempfile();

                    my $command = "slaughter --verbose 2>&1 >> $filename";
                    system( $command );

                    $c->send_file_response( $filename );

                    unlink( $filename );

                }
                else
                {
                    $c->send_error(RC_FORBIDDEN)
                }
            }
            else
            {
                $c->send_error(RC_FORBIDDEN)
            }
        }
        else
        {
            $c->send_error(RC_FORBIDDEN)
        }
    }
    $c->close;
    undef($c);
}



=begin doc

Read the system-wide secret file.

=end doc

=cut

sub readFile
{
    my( $file ) = ( @_ );

    if ( ! -e $file )
    {
        print "Secret file not present\n";
        exit 1;
    }

    open my $handle, "<", $file
      or die "Failed to read secret file $file - $!";

    my $line = <$handle>;
    chomp( $line ) if ( defined( $line ) );
    close( $handle );

    return( $line );

}
